//-------------------------------------------------------------------------------------------------------
// Copyright (C) Microsoft. All rights reserved.
// Licensed under the MIT license. See LICENSE.txt file in the project root for full license information.
//-------------------------------------------------------------------------------------------------------

import "wtypes.idl";

// TODO: OOP JIT, how do we make this better?
const int VTABLE_COUNT = 40;

typedef struct ObjTypeSpecFldData
{
    // TODO: OOP JIT we may want to copy some of the data in these pointers
    __int3264 protoObject;
    __int3264 propertyGuard;
    __int3264 typeSet;
    __int3264 initialType;
    __int3264 ctorCache;
    __int3264 fixedFieldInfoArray;

    int propertyId;
    int typeId;
    unsigned int id;

    unsigned short flags;
    unsigned short slotIndex;
    unsigned short fixedFieldCount;
} ObjTypeSpecFldData;

typedef struct BVUnitData
{
    unsigned __int3264 word;
} BVUnitData;

typedef struct BVFixedData
{
    unsigned int len;
    [size_is(len)] BVUnitData data[*];
} BVFixedData;

typedef struct CallSiteData
{
    unsigned short bitFields;
    unsigned short returnType;
    unsigned int ldFldInlineCacheId;
    unsigned int sourceId;
    unsigned int functionId;
} CallSiteData;

typedef struct ThisData
{
    unsigned short valueType;
    byte thisType;
} ThisData;

typedef struct FldData
{
    unsigned short valueType;
    byte flags;
    byte polymorphicInlineCacheUtilization;
} FldData;

typedef struct ArrayCallSiteData
{
    byte bits;
    unsigned int functionNumber;
    unsigned short callSiteNumber;
} ArrayCallSiteData;

typedef struct LdElemData
{
    unsigned short arrayType;
    unsigned short elemType;
    byte bits;
} LdElemData;

typedef struct StElemData
{
    unsigned short arrayType;
    byte bits;
} StElemData;

typedef struct ProfileData
{
    int flags;

    BVFixedData * loopFlags;

    unsigned short profiledLdElemCount;
    [size_is(profiledLdElemCount)] LdElemData * ldElemData;

    unsigned short profiledStElemCount;
    [size_is(profiledStElemCount)] StElemData * stElemData;

    unsigned short profiledArrayCallSiteCount;
    [size_is(profiledArrayCallSiteCount)] ArrayCallSiteData * arrayCallSiteData;

    // TODO: michhol OOP JIT, share counts with body
    unsigned int inlineCacheCount;
    [size_is(inlineCacheCount)] FldData * fldData;

    unsigned short profiledSlotCount;
    [size_is(profiledSlotCount)] unsigned short * slotData;

    unsigned short profiledCallSiteCount;
    [size_is(profiledCallSiteCount)] CallSiteData * callSiteData;

    unsigned short profiledReturnTypeCount;
    [size_is(profiledReturnTypeCount)] unsigned short * returnTypeData;

    unsigned short profiledDivOrRemCount;
    [size_is(profiledDivOrRemCount)] unsigned short * divideTypeInfo;

    unsigned short profiledSwitchCount;
    [size_is(profiledSwitchCount)] unsigned short * switchTypeInfo;

    unsigned short profiledInParamsCount;
    [size_is(profiledInParamsCount)] unsigned short * parameterInfo;

    unsigned int loopCount;
    [size_is(loopCount)] byte * loopImplicitCallFlags;

    ThisData thisData;

    byte implicitCallFlags;

} ProfileData;

typedef struct ThreadContextData
{
    boolean isThreadBound;
    __int3264 processHandle;
    __int3264 chakraBaseAddress;
    __int3264 crtBaseAddress;
    __int3264 threadStackLimitAddr;
    __int3264 scriptStackLimit;
    __int3264 bailOutRegisterSaveSpace;
    __int3264 disableImplicitFlagsAddr;
    __int3264 implicitCallFlagsAddr;
    __int3264 debuggingFlagsAddr;
    __int3264 debugStepTypeAddr;
    __int3264 debugFrameAddressAddr;
    __int3264 debugScriptIdWhenSetAddr;
} ThreadContextData;

typedef struct ScriptContextData
{
    boolean isRecyclerVerifyEnabled;
    unsigned int recyclerVerifyPad;
    __int3264 vtableAddresses[VTABLE_COUNT];

    __int3264 nullAddr;
    __int3264 undefinedAddr;
    __int3264 trueAddr;
    __int3264 falseAddr;
    __int3264 undeclBlockVarAddr;
    __int3264 scriptContextAddr;
    __int3264 emptyStringAddr;
    __int3264 negativeZeroAddr;
    __int3264 numberTypeStaticAddr;
    __int3264 stringTypeStaticAddr;
    __int3264 objectTypeAddr;
    __int3264 objectHeaderInlinedTypeAddr;
    __int3264 regexTypeAddr;
    __int3264 arrayTypeAddr;
    __int3264 nativeIntArrayTypeAddr;
    __int3264 nativeFloatArrayTypeAddr;
    __int3264 arrayConstructorAddr;
    __int3264 charStringCacheAddr;
    __int3264 libraryAddr;
    __int3264 sideEffectsAddr;
    __int3264 arraySetElementFastPathVtableAddr;
    __int3264 intArraySetElementFastPathVtableAddr;
    __int3264 floatArraySetElementFastPathVtableAddr;
    __int3264 numberAllocatorAddr;
    __int3264 recyclerAddr;
} ScriptContextData;

typedef struct SmallSpanSequenceData
{
    int baseValue;
    unsigned int statementLength;
    [size_is(statementLength)] unsigned int * statementBuffer;
    unsigned int actualOffsetLength; // REVIEW: are lengths the same?
    [size_is(actualOffsetLength)] unsigned int * actualOffsetList;
} SmallSpanSequenceData;

typedef struct JITLoopHeader
{
    boolean isNested;
    boolean isInTry;
    unsigned int interpretCount;
    unsigned int startOffset;
    unsigned int endOffset;
} JITLoopHeader;

typedef struct AsmJsJITData
{
    boolean isHeapBufferConst;
    boolean usesHeapBuffer;
    unsigned short argByteSize;
    unsigned short argCount;
    [size_is(argCount)] byte * argTypeArray;
    int retType;
    int intConstCount;
    int doubleConstCount;
    int floatConstCount;
    int simdConstCount;
    int intTmpCount;
    int doubleTmpCount;
    int floatTmpCount;
    int simdTmpCount;
    int intVarCount;
    int doubleVarCount;
    int floatVarCount;
    int simdVarCount;
    int intByteOffset;
    int doubleByteOffset;
    int floatByteOffset;
    int simdByteOffset;
    int totalSizeInBytes;
} AsmJsJITData;

typedef struct FunctionJITRuntimeData
{
    unsigned int clonedCacheCount;
    [size_is(clonedCacheCount)] __int3264 * clonedInlineCaches;
} FunctionJITRuntimeData;

// FunctionBody fields, read only in JIT, gathered in foreground
typedef struct FunctionBodyJITData
{
    byte flags;

    // TODO: compress booleans into flags
    boolean doBackendArgumentsOptimization;
    boolean isLibraryCode;
    boolean isAsmJsMode;
    boolean hasImplicitArgIns;
    boolean isStrictMode;
    boolean isEval;
    boolean hasScopeObject;
    boolean hasCachedScopePropIds;
    boolean inlineCachesOnFunctionObject;
    boolean doInterruptProbe;
    boolean isGlobalFunc;
    boolean doJITLoopBody;
    boolean disableInlineSpread;
    boolean hasNestedLoop;
    boolean hasNonBuiltInCallee;

    unsigned short envDepth;
    unsigned short inParamCount;
    unsigned short profiledIterations;
    unsigned short argUsedForBranch;
    unsigned short profiledCallSiteCount;

    unsigned int funcNumber;
    unsigned int sourceContextId;
    unsigned int nestedCount;
    unsigned int scopeSlotArraySize;
    unsigned int attributes;
    unsigned int byteCodeCount;
    unsigned int byteCodeInLoopCount;
    unsigned int nonLoadByteCodeCount;
    unsigned int localFrameDisplayReg;
    unsigned int localClosureReg;
    unsigned int envReg;
    unsigned int firstTmpReg;
    unsigned int firstInnerScopeReg;
    unsigned int varCount;
    unsigned int innerScopeCount;
    unsigned int thisRegisterForEventHandler;
    unsigned int funcExprScopeRegister;
    unsigned int loopCount;
    unsigned int recursiveCallSiteCount;
    unsigned int isInstInlineCacheCount; // TODO: only used in Assert

    SmallSpanSequenceData statementMap;

    unsigned int byteCodeLength;
    [size_is(byteCodeLength)] byte * byteCodeBuffer;

    unsigned int constCount;
    [size_is(constCount)] __int3264 * constTable;
    unsigned int constTypeCount;
    [size_is(constTypeCount)] int * constTypeTable;

    unsigned int inlineCacheCount;
    [size_is(inlineCacheCount)] int * cacheIdToPropertyIdMap;
    [size_is(inlineCacheCount)] __int3264 * inlineCaches;

    unsigned int loopHeaderArrayLength;
    [size_is(loopHeaderArrayLength)] JITLoopHeader * loopHeaders;

    unsigned int referencedPropertyIdCount;
    [size_is(referencedPropertyIdCount)] int * referencedPropertyIdMap;

    unsigned int nameLength;
    [size_is(nameLength)] wchar_t * displayName;

    unsigned int runtimeDataCount; // only needed for topfunc?
    [size_is(runtimeDataCount)] FunctionJITRuntimeData * profiledRuntimeData;

    ProfileData * profileData;

    AsmJsJITData * asmJsData;

    __int3264 loopHeaderArrayAddr;
    __int3264 functionBodyAddr;
    __int3264 scriptIdAddr;
    __int3264 probeCountAddr;
    __int3264 flagsAddr;
    __int3264 regAllocStoreCountAddr;
    __int3264 regAllocLoadCountAddr;
    __int3264 callCountStatsAddr;
    __int3264 nestedFuncArrayAddr;
} FunctionBodyJITData;

// EntryPointInfo fields, read/write in JIT
typedef struct EntryPointInfoJITWriteData
{
    boolean hasJittedStackClosure;
    int localVarSlotsOffset; // FunctionEntryPointInfo only
    int localVarChangedOffset; // FunctionEntryPointInfo only
    unsigned int frameHeight;
} EntryPointInfoJITWriteData;

// FunctionBody fields, written to from JIT
typedef struct FunctionBodyJITWriteData
{
    boolean hasBailoutInstr;
    unsigned short argUsedForBranch;
} FunctionBodyJITWriteData;

typedef struct FunctionJITTimeData
{
    boolean isAggressiveInliningEnabled;
    boolean isInlined;
    unsigned int localFuncId;
    __int3264 functionInfoAddr;
    __int3264 callsCountAddress;
    __int3264 weakFuncRef;
    FunctionBodyJITData * bodyData; // TODO: oop jit, can these repeat, should we share?

    BVFixedData * inlineesBv;

    unsigned int inlineeCount;
    [size_is(inlineeCount)] struct FunctionJITTimeData ** inlinees;
    unsigned int ldFldInlineeCount;
    [size_is(ldFldInlineeCount)] struct FunctionJITTimeData ** ldFldInlinees;
    // ldFldInlineeCount == inlineCacheCount
    [size_is(ldFldInlineeCount)] ObjTypeSpecFldData ** objTypeSpecFldInfoArray;
    struct FunctionJITTimeData * next;
} FunctionJITTimeData;

typedef struct XProcNumberPageSegment
{
    __int3264 pageAddress;
    __int3264 allocStartAddress;
    __int3264 allocEndAddress;
    __int3264 pageSegment;
    __int3264 chunkAllocator;
    struct XProcNumberPageSegment* nextSegment;
    unsigned int blockSize; // 2 pages
    unsigned int committedEnd;
    unsigned int pageCount; // REVIEW: don't actually need these two fields, they are constant
    unsigned int sizeCat;
    unsigned int blockIntegratedSize;
} XProcNumberPageSegment;

// CodeGenWorkItem fields, read only in JIT
typedef struct CodeGenWorkItemJITData
{
    boolean isJitInDebugMode;  // Whether JIT is in debug mode for this work item.
    byte type;
    char jitMode;

    unsigned int loopNumber;
    XProcNumberPageSegment xProcNumberPageSegment;

    FunctionJITTimeData * jitData;
    __int3264 nativeDataAddr;
} CodeGenWorkItemJITData;

typedef struct NativeDataFixupEntry
{
    struct NativeDataFixupEntry* next;
    unsigned int addrOffset;
    unsigned int targetTotalOffset;
} NativeDataFixupEntry;

typedef struct NativeDataFixupRecord
{
    unsigned int index;
    unsigned int length;
    unsigned int startOffset;
    struct NativeDataFixupEntry* updateList;
} NativeDataFixupRecord;

typedef struct NativeDataFixupTable
{
    unsigned int count;
    [size_is(count)] NativeDataFixupRecord fixupRecords[*];
} NativeDataFixupTable;

typedef struct NativeDataBuffer
{
    unsigned int len;
    unsigned int unused;
    [size_is(len)] byte data[*];

} NativeDataBuffer;

// Fields that JIT modifies
typedef struct JITOutputData
{
    boolean isInPrereservedRegion;

    unsigned short pdataCount;
    unsigned short xdataSize;

    EntryPointInfoJITWriteData writeableEPData;
    FunctionBodyJITWriteData writeableBodyData;

    unsigned int codeSize;

    NativeDataFixupTable* nativeDataFixupTable;
    NativeDataBuffer* buffer;

    __int64 codeAddress;
    __int64 xdataAddr;

    XProcNumberPageSegment* numberPageSegments;
    
    unsigned int inlineeFrameOffsetArrayOffset;
    unsigned int inlineeFrameOffsetArrayCount;
} JITOutputData;

[
    uuid(ead694ed-2243-44cb-a9dc-85d3ba934dab),
    pointer_default(unique)
]
interface IChakraJIT
{
    HRESULT Shutdown([in] handle_t binding);

    HRESULT InitializeThreadContext(
        [in] handle_t binding,
        [in] ThreadContextData * threadData,
        [out] __int3264 * threadContextInfoAddress);

    HRESULT CleanupThreadContext(
        [in] handle_t binding,
        [in] __int3264 threadContextInfoAddress);

    HRESULT InitializeScriptContext(
        [in] handle_t binding,
        [in] ScriptContextData * scriptContextData,
        [out] __int3264 * scriptContextInfoAddress);

    HRESULT CleanupScriptContext(
        [in] handle_t binding,
        [in] __int3264 scriptContextInfoAddress);

    HRESULT FreeAllocation(
        [in] handle_t binding,
        [in] __int3264 threadContextInfoAddress,
        [in] __int3264 address);

    HRESULT RemoteCodeGen(
        [in] handle_t binding,
        [in] __int3264 threadContextInfoAddress,
        [in] __int3264 scriptContextInfoAddress,
        [in] CodeGenWorkItemJITData * workItemData,
        [out] JITOutputData * jitData);

}
